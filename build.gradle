/*
 * Copyright 2015 The kdtree authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import proguard.gradle.ProGuardTask

apply plugin: 'scala'
apply plugin: "maven-publish"

group = "org.asoem"
version = "1.2.7"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'org.scala-lang:scala-library:2.10.4'
    testCompile 'org.scalatest:scalatest_2.10:2.2.1', "junit:junit:4.11"
}

compileScala {
    compileScala.scalaCompileOptions.additionalParameters = ['-target:jvm-1.6']
}

compileTestScala {
    compileTestScala.scalaCompileOptions.additionalParameters = ['-target:jvm-1.6']
}

test << {
    ant.taskdef(name: 'scalatest', classname: 'org.scalatest.tools.ScalaTestAntTask', classpath: classpath.asPath)
    ant.scalatest(runpath: testClassesDir, haltonfailure: 'true', fork: 'false') {
        reporter(type: 'stderr')
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:5.2.1'
    }
}

task proguard(type: ProGuardTask) {
    dependsOn jar

    ext {
        outDir = file("${buildDir}/proguard")
        destFile = "${outDir}/${jar.baseName}-${jar.version}.proguard.${jar.extension}"
    }
    outDir.mkdirs()

    injars jar.archivePath
    configurations.runtime.each { file ->
        injars file.path, filter: '!META-INF/MANIFEST.MF'
    }
    outjars ext.destFile
    libraryjars "${System.getProperty('java.home')}/lib/rt.jar"

    configuration 'proguard.conf'

    doLast {
        logger.lifecycle "[Proguard] Generated processed JAR in ${destFile}"
    }
}

task proguardJar(type: Jar) {
    dependsOn proguard
    from file(proguard.destFile)
}

/*
artifacts {
    archives proguardJar
}
*/

/* In Gradle 1.6 it is not possible to change the artifactId. Maybe in future versions: https://github.com/gradle/gradle/blob/master/design-docs/publication-model.md#customising-maven-descriptor-xml
publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
        maven(MavenPublication) {
            artifact proguardJar
            pom.withXml {
                asNode().artifactId[0].value = 'kdtree-scala'
                def nodeToDel = asNode().dependencies.dependency.find {
                    it.groupId.text() == 'org.scala-lang' && it.artifactId.text() == 'scala-library'
                }
                if (nodeToDel != null) {
                    def parent = nodeToDel.parent()
                    parent.remove(nodeToDel)
                }
                else
                    logger.warn "Couldn't delete dependency org.scala-lang:scala-library"
            }
        }
    }
}
*/